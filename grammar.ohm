Kaashi {
  Program = Import* Expr


  Import = Extract_import | Alias_import
  Extract_import = from_directive address import name (arg_sep name)*
  Alias_import = import_directive address as name

  from_directive = "@from"
  import_directive = "@import"
  as = "as"
  import = "import"
  address = (alnum
       | "+" | "-" | "=" | "&" | "?" | "." | ":" | "," | ";"
       | "[" | "]" | "/" | "\\" | "#" | "@" | "!" | "~" | "%"
       | "'" | "\""
       )+


  Expr = Block
       | Pattern
       | Lambda
       | Pipeline
       | Operation
       | Access
       | Paranthesis
       | Atomic


  Block = open_block (Rule block_sep)* Rule? close_block

  Rule = Parametric_rule
       | Parametric_extension_rule
       | Key_rule
       | Key_extension_rule
       | Constant_rule
       | Constant_extension_rule
       | Expr

  Parametric_extension_rule = Args+ extend Expr
  Key_extension_rule = name extend Expr
  Constant_extension_rule = Value extend Expr

  Parametric_rule = name? Args+ define Expr
  Key_rule = name define Expr
  Constant_rule = Value define Expr


  Pattern = open_block (Matching block_sep)* Matching? close_block
  Matching = Expr Condition
  Condition = subject_to (Expr | otherwise)


  Lambda = Args+ define Expr

  Args = open_index (Arg arg_sep)* Arg close_index
  Arg = Destructuring_arg
      | Variable_arg
      | Constant_arg

  Destructuring_arg = Destructuring (subject_to Expr)?
  Destructuring = open_block (Destructuring_element arg_sep)* (Destructuring_element)? close_block
  Destructuring_element = Destructuring | destructuring_name | name
  destructuring_name = rest name

  Variable_arg = name Default_value? (subject_to Expr)?
  Default_value = open_paran default Expr close_paran
  Constant_arg = Value


  Pipeline = (Pipeline | Pipeline_operand) pipe Pipeline_operand
  Pipeline_operand = Operation | Operand

  Operation = Operand (Operation | Operand)
  Operand = ~pipe
          ( Access
          | Atomic
          | Paranthesis
          )
  
  Access = Index | Attr
  Attr = (Access | Index_operand) access name
  Index = (Access | Index_operand) open_index (Expr arg_sep)* Expr close_index
  Index_operand = Atomic
                | Paranthesis
  
  Paranthesis = open_paran Expr close_paran

  Atomic = variable | Value | env | operator
  name = variable | operator
  
  env = "$" variable
  variable = ~reserved (var_letter (var_letter | alnum)*)
  var_letter = "_" | letter

  Value = boolean | number | String

  String = Template_string
         | single_quote_string
         | dbl_quote_string

  Template_string = Template | bland_template
  Template = template_start Expr (template_middle Expr)* template_end
  
  template_middle = close_block (~(backtick | open_block) any)* open_block
  template_end = close_block (~(backtick | open_block) any)* backtick
  template_start = backtick (~(backtick | open_block) any)* open_block
  bland_template = backtick (~backtick any)* backtick
  
  single_quote_string = single_quote (~single_quote any)* single_quote
  dbl_quote_string = dbl_quote (~dbl_quote any)* dbl_quote
  
  single_quote = "'"
  dbl_quote = "\""
  backtick = "`"
  
  number = "-"? (bto | decimal)
  bto = ddigit+ decimal?
  decimal = "." ddigit+
  ddigit = digit
         | "_"
  
  boolean = true
          | false
  
  operator = ~pipe
           ( rest
           | is | and | or | not
           | "==" | "!=" | "=>" | "<=" | ">=" | "=<" | "&&" | "||" | "++" | "--" | "**"
           | ">>" | "<<" | "^^" | "??" | "!!"
           | "+" | "-" | "*" | "/" | "=" | "!" | "&" | "%" | "~" | "?" | ">" | "<"
           | "^"
           )
  
  otherwise = "otherwise"
  and = "and"
  or = "or"
  not = "not"
  is = "is"
  true = "true"
  false = "false"
  default = "default"
  reserved = otherwise | and | or | not | is | true | false | default
  
  block_sep = ";" | ","
  arg_sep = ","
  subject_to = "|"
  open_block = "{"
  close_block = "}"
  open_index = "["
  close_index = "]"
  open_paran = "("
  close_paran = ")"
  define = ":"
  extend = "::"
  access = "."
  rest = "..."
  pipe = "->"
  space := " " | "\t" | "\r" | "\n" | comment
  comment = line_comment | multi_comment | endless_comment
  line_comment = "//" (~"\n" any)*
  multi_comment = "/*" (~"*/" any)* "*/"
  endless_comment = "/*" (~"*/" any)*
}